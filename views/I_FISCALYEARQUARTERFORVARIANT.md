---
name: I_FISCALYEARQUARTERFORVARIANT
description: Fiscalyearquarterforvariant
app_component: CA-GTF-GL-CAL
software_component: SAP_BASIS
release_state: released
clean_core_level: A
system_type: public_cloud
source_available: true
tags:
  - CA
  - CA-GTF
  - CA-GTF-GL
  - interface-view
  - component:CA-GTF-GL-CAL
  - lob:Cross-Application Components
---
# I_FISCALYEARQUARTERFORVARIANT

**Fiscalyearquarterforvariant**

| Property | Value |
|---|---|
| App Component | `CA-GTF-GL-CAL` |
| Software Component | `SAP_BASIS` |
| Release State | Released (Level A) |
| System Type | S/4HANA Cloud Public Edition |

## Fields

| Field | Data Source |
|---|---|
| `FiscalYearVariant` | `P_FiscalYearQuarter.FiscalYearVariant` |
| `FiscalYear` | `P_FiscalYearQuarter.FiscalYear` |
| `FiscalYearQuarter` | `P_FiscalYearQuarter.FiscalYearQuarter` |
| `FiscalYearStartDate` | `P_FiscalYearQuarter.FiscalYearStartDate` |
| `FiscalYearEndDate` | `P_FiscalYearQuarter.FiscalYearEndDate` |
| `FiscalQuarter` | `P_FiscalYearQuarter.FiscalQuarter` |
| `FiscalQuarterStartDate` | `P_FiscalYearQuarter.FiscalQuarterStartDate` |
| `FiscalQuarterEndDate` | `P_FiscalYearQuarter.FiscalQuarterEndDate` |
| `FiscalQuarterConsecutiveNumber` | `P_FiscalYearQuarter.FiscalQuarterConsecutiveNumber` |
| `NextFiscalQuarter` | `cast(…)` |
| `NextFiscalYear` | `cast(…)` |
| `NextFiscalYearQuarter` | `cast(…)` |
| `NextFiscalQuarterStartDate` | `cast(…)` |
| `NextFiscalQuarterEndDate` | `cast(…)` |
| `NextFsclQuarterConsecutiveNmbr` | `cast(…)` |
| `_FiscalYearVariant` | *Association* |
| `_FiscalYear` | *Association* |

## Associations

| Alias | Target View | Cardinality |
|---|---|---|
| `_FiscalYear` | `I_FiscalYear` | [0..1] |

## Source Code

```abap
@VDM.lifecycle.contract.type: #PUBLIC_LOCAL_API
@Metadata.ignorePropagatedAnnotations: true
@EndUserText.label: 'Fiscal Year Quarter with Next Fiscal Year Quarter'
@VDM.viewType: #BASIC

@ObjectModel.usageType.serviceQuality: #C
@ObjectModel.usageType.sizeCategory: #M
@ObjectModel.usageType.dataClass: #CUSTOMIZING

@AccessControl.authorizationCheck: #NOT_REQUIRED
@ObjectModel.representativeKey: 'FiscalYearQuarter'
@Analytics.technicalName: 'IFIFYEARQU4VAR'
//@Analytics: { dataCategory: #DIMENSION, dataExtraction.enabled: true }
@Analytics: { dataCategory: #DIMENSION }

@Analytics.internalName: #LOCAL

@ObjectModel.modelingPattern: #ANALYTICAL_DIMENSION
@ObjectModel.supportedCapabilities: [#ANALYTICAL_DIMENSION, #CDS_MODELING_ASSOCIATION_TARGET, #SQL_DATA_SOURCE, #CDS_MODELING_DATA_SOURCE]

define view entity I_FiscalYearQuarterForVariant as select from P_FiscalYearQuarter 

association [0..1] to I_FiscalYear as _FiscalYear on  $projection.FiscalYearVariant     = _FiscalYear.FiscalYearVariant
                                                  and $projection.FiscalYear            = _FiscalYear.FiscalYear
{
  
@ObjectModel.foreignKey.association: '_FiscalYearVariant'      
key P_FiscalYearQuarter.FiscalYearVariant,

@ObjectModel.foreignKey.association: '_FiscalYear'      
key P_FiscalYearQuarter.FiscalYear,

key P_FiscalYearQuarter.FiscalYearQuarter, 

    P_FiscalYearQuarter.FiscalYearStartDate,
    P_FiscalYearQuarter.FiscalYearEndDate,
        
    P_FiscalYearQuarter.FiscalQuarter, 
    P_FiscalYearQuarter.FiscalQuarterStartDate,
    P_FiscalYearQuarter.FiscalQuarterEndDate,

    P_FiscalYearQuarter.FiscalQuarterConsecutiveNumber,
      
    cast( P_FiscalYearQuarter.NextFiscalQuarter as fins_next_fiscalquarter preserving type )                                                  as NextFiscalQuarter,
    cast( cast( substring(P_FiscalYearQuarter.NextFiscalYearQuarter,1,4) as abap.numc(4) ) as fins_next_fiscalyear_no_conv preserving type )  as NextFiscalYear,

    cast( P_FiscalYearQuarter.NextFiscalYearQuarter as fins_next_fyearquarter preserving type )                                               as NextFiscalYearQuarter,
    cast( P_FiscalYearQuarter.NextFiscalQuarterStartDate as fins_next_fquarter_startdate preserving type )                                    as NextFiscalQuarterStartDate,
    cast( P_FiscalYearQuarter.NextFiscalQuarterEndDate as fins_next_fquarter_enddate preserving type )                                        as NextFiscalQuarterEndDate,

    cast( P_FiscalYearQuarter.NextFsclQuarterConsecutiveNmbr as fins_next_fyearquarter_i preserving type )                                    as NextFsclQuarterConsecutiveNmbr,

    P_FiscalYearQuarter._FiscalYearVariant,
    _FiscalYear
    
}
```
