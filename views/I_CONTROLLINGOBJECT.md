---
name: I_CONTROLLINGOBJECT
description: Controllingobject
semantic_en: "controlling object — master data identifying controlling objects (orders, cost centers, cost elements, projects, WBS) and their relationships in controlling areas."
semantic_vi: "đối tượng kiểm soát — dữ liệu chính xác định các đối tượng kiểm soát (đơn hàng, trung tâm chi phí, yếu tố chi phí, dự án, WBS) và quan hệ của chúng trong vùng kiểm soát."
keywords:
  - đối tượng kiểm soát
  - trung tâm chi phí
  - dự án
app_component: FI-GL-IS-2CL
software_component: SAPSCORE
release_state: released
clean_core_level: A
system_type: public_cloud
source_available: true
tags:
  - FI
  - FI-GL
  - FI-GL-IS
  - interface-view
  - component:FI-GL-IS-2CL
  - lob:Finance
  - bo:ControllingObject
---
# I_CONTROLLINGOBJECT

**Controllingobject**

| Property | Value |
|---|---|
| App Component | `FI-GL-IS-2CL` |
| Software Component | `SAPSCORE` |
| Release State | Released (Level A) |
| System Type | S/4HANA Cloud Public Edition |

## Fields

| Field | Data Source |
|---|---|
| `ControllingObject` | `onr00.objnr` |
| `OrderID` | `onror.aufnr` |
| `BusinessProcess` | `onrbp.prznr` |
| `CostCtrActivityType` | `onrkl.lstar` |
| `SalesDocument` | `onrvb.vbeln` |
| `SalesDocumentItem` | `onrvb.posnr` |
| `ControllingArea` | `cast(…)` |
| `CostCenter` | `cast( COALESCE(onrks.kostl, onrkl.kostl) as fis_kostl )` |
| `WBSElementInternalID` | `cast( onrpr.pspnr as fis_wbsint_no_conv preserving type )` |
| `ProjectInternalID` | `cast( onrpd.pspnr as fis_projectint_no_conv preserving type)` |
| `ControllingObjectType` | `cast ( SUBSTRING( onr00.objnr, 1, 2) as j_obart)` |
| `_ControllingArea` | *Association* |
| `_CostCenter` | *Association* |
| `_CostCtrActivityType` | *Association* |

## Associations

| Alias | Target View | Cardinality |
|---|---|---|
| `_ControllingArea` | `I_ControllingArea` | [1] |
| `_CostCenter` | `I_CostCenter` | [0..*] |
| `_CostCtrActivityType` | `I_CostCenterActivityType` | [0..*] |

## Source Code

```abap
@EndUserText.label: 'Controlling Object'
@Analytics: { dataCategory: #DIMENSION, dataExtraction.enabled: true }
@VDM.viewType: #BASIC
@AbapCatalog.sqlViewName: 'IFICOOBJECT'
@AccessControl.authorizationCheck: #CHECK //NOT_REQUIRED
@ObjectModel.representativeKey: 'ControllingObject'
@ClientHandling.algorithm: #SESSION_VARIABLE

@ObjectModel.supportedCapabilities: [ #EXTRACTION_DATA_SOURCE, #ANALYTICAL_DIMENSION, #CDS_MODELING_ASSOCIATION_TARGET, #SQL_DATA_SOURCE, #CDS_MODELING_DATA_SOURCE ]
@ObjectModel.modelingPattern: #ANALYTICAL_DIMENSION 

@ObjectModel.usageType: {
  dataClass: #TRANSACTIONAL,
  serviceQuality: #C,
  sizeCategory: #L
}
@Metadata.allowExtensions: true
@Metadata.ignorePropagatedAnnotations: true

define view I_ControllingObject
// Corresponds to calculation view 'sap.hba.ecc.ControllingObject'
as select from onr00

left outer join onrks on  onr00.objnr = onrks.objnr
left outer join onrbp on  onr00.objnr = onrbp.objnr
left outer join onrkl on  onr00.objnr = onrkl.objnr
left outer join onror on  onr00.objnr = onror.objnr
//left outer join onrov on  Join_4.objnr = onrov.objnr
left outer join onrpd on  onr00.objnr = onrpd.objnr
left outer join onrpr on  onr00.objnr = onrpr.objnr
left outer join onrvb on  onr00.objnr = onrvb.objnr

association[1]          to I_ControllingArea              as _ControllingArea         
    on  $projection.ControllingArea         = _ControllingArea.ControllingArea
association [0..*]      to I_CostCenter                   as _CostCenter             
    on  $projection.CostCenter              = _CostCenter.CostCenter
    and $projection.ControllingArea         = _CostCenter.ControllingArea
association[0..*]       to I_CostCenterActivityType       as _CostCtrActivityType     
    on  $projection.CostCtrActivityType     = _CostCtrActivityType.CostCtrActivityType
    and $projection.ControllingArea         = _CostCtrActivityType.ControllingArea    
{

  
  
  key   onr00.objnr as ControllingObject,

        onror.aufnr as OrderID,

        onrbp.prznr as BusinessProcess,

        onrkl.lstar as CostCtrActivityType,

        onrvb.vbeln as SalesDocument,

        onrvb.posnr as SalesDocumentItem,
 
        cast ( COALESCE ( COALESCE( COALESCE(onrks.kokrs, onrbp.kokrs ),   onrkl.kokrs), '')  as fis_kokrs ) as ControllingArea,

        cast( COALESCE(onrks.kostl, onrkl.kostl) as fis_kostl ) as CostCenter,

        cast( onrpr.pspnr as fis_wbsint_no_conv preserving type ) as WBSElementInternalID,
        cast( onrpd.pspnr as fis_projectint_no_conv preserving type) as ProjectInternalID,
        cast ( SUBSTRING( onr00.objnr, 1, 2) as j_obart) as ControllingObjectType,
        _ControllingArea ,
        _CostCenter ,
        _CostCtrActivityType  
        
};
```
