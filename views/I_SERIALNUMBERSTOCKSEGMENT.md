---
name: I_SERIALNUMBERSTOCKSEGMENT
description: Serial NumberSTOCKSEGMENT
semantic_en: "transaction data — serial number inventory positions across plants, storage locations, and stock types."
semantic_vi: "dữ liệu tồn kho của số seri theo nhà máy, kho và loại tồn kho."
keywords:
  - số seri
  - tồn kho
  - nhà máy
app_component: LO-MD-SN-2CL
software_component: SAPSCORE
release_state: released
clean_core_level: A
system_type: public_cloud
source_available: true
tags:
  - LO
  - LO-MD
  - LO-MD-SN
  - interface-view
  - serial-number
  - component:LO-MD-SN-2CL
  - lob:Logistics General
  - bo:SerialNumber
---
# I_SERIALNUMBERSTOCKSEGMENT

**Serial NumberSTOCKSEGMENT**

| Property | Value |
|---|---|
| App Component | `LO-MD-SN-2CL` |
| Software Component | `SAPSCORE` |
| Release State | Released (Level A) |
| System Type | S/4HANA Cloud Public Edition |

## Fields

| Field | Data Source |
|---|---|
| `Material` | `e.Material` |
| `SerialNumber` | `e.SerialNumber` |
| `Equipment` | `ess.Equipment` |
| `Plant` | `ess.Plant` |
| `StorageLocation` | `ess.StorageLocation` |
| `Customer` | `ess.Customer` |
| `Supplier` | `ess.Supplier` |
| `SalesOrder` | `ess.SalesOrder` |
| `SalesOrderItem` | `ess.SalesOrderItem` |
| `InventorySpecialStockType` | `ess.InventorySpecialStockType` |
| `WBSElementInternalID` | `ess.WBSElementInternalID` |
| `Batch` | `ess.Batch` |
| `InventoryStockType` | `ess.InventoryStockType` |
| `StockOwner` | `ess.StockOwner` |
| `_Material` | *Association* |
| `_Equipment` | *Association* |
| `_Plant` | *Association* |
| `_StorageLocation` | *Association* |
| `_SalesOrder` | *Association* |
| `_SalesOrderItem` | *Association* |
| `_Customer` | *Association* |
| `_CustomerCompanyByPlant` | *Association* |
| `_Supplier` | *Association* |
| `_SupplierCompanyByPlant` | *Association* |
| `_InventoryStockType` | *Association* |
| `_InventorySpecialStockType` | *Association* |
| `_WBSElementByInternalKey` | *Association* |

## Associations

> No associations found.

## Source Code

```abap
@EndUserText.label: 'Serial Number Stock Segment'
 
@VDM.viewType: #BASIC
@AccessControl.authorizationCheck: #CHECK 
@AccessControl.personalData.blocking: #REQUIRED 
@ObjectModel.representativeKey: 'SerialNumber'
@Analytics.technicalName: 'ISERNUMSTOCKSEG'
@ObjectModel.semanticKey:  [ 'Material', 'SerialNumber' ] 
@ObjectModel.supportedCapabilities: [
  #SQL_DATA_SOURCE,
  #CDS_MODELING_DATA_SOURCE,
  #CDS_MODELING_ASSOCIATION_TARGET,
  #ANALYTICAL_DIMENSION
]

@ObjectModel.usageType.dataClass: #MASTER
@ObjectModel.usageType.serviceQuality: #A
@ObjectModel.usageType.sizeCategory: #L

@Analytics: {dataCategory: #DIMENSION }

@Metadata.allowExtensions: true
@Metadata.ignorePropagatedAnnotations: true

define view entity I_SerialNumberStockSegment 
as select from I_EquipmentStockSegment as ess
  inner join I_Equipment as e
    on ess.Equipment = e.Equipment
{
  @ObjectModel.foreignKey.association: '_Material'
  key e.Material,
  
  key e.SerialNumber,
  
  @ObjectModel.foreignKey.association: '_Equipment'
  ess.Equipment,
         
  @ObjectModel.foreignKey.association: '_Plant'
  ess.Plant,   
  
  @ObjectModel.foreignKey.association: '_StorageLocation'
  ess.StorageLocation, 
     
  @ObjectModel.foreignKey.association: '_Customer'
  @Consumption.valueHelpDefinition: [
    { entity:  { name:    'I_Customer_VH',
                 element: 'Customer' }
    }]
  ess.Customer, 
  
  @ObjectModel.foreignKey.association: '_Supplier'
  @Consumption.valueHelpDefinition: [
    { entity:  { name:    'I_Supplier_VH',
                 element: 'Supplier' }
    }]
  ess.Supplier,  
  
  @ObjectModel.foreignKey.association: '_SalesOrder'
  @Consumption.valueHelpDefinition: [
    { entity:  { name:    'I_SalesOrderStdVH',
                 element: 'SalesOrder' }
    }]
  ess.SalesOrder, 
  
  @ObjectModel.foreignKey.association: '_SalesOrderItem'
  @Consumption.valueHelpDefinition: [
    { entity:  { name:    'I_SalesOrderItemStdVH',
                 element: 'SalesOrderItem' },
      additionalBinding: [{ localElement: 'SalesOrder',
                            element: 'SalesOrder' }]
    }]
  ess.SalesOrderItem,  
  
  @ObjectModel.foreignKey.association: '_InventorySpecialStockType'
  ess.InventorySpecialStockType,
    
  @ObjectModel.foreignKey.association: '_WBSElementByInternalKey'
  @Consumption.valueHelpDefinition: [
    { entity:  { name:    'I_WBSElmntByIntKeyStdVH',
                 element: 'WBSElementInternalID' }
    }]
  ess.WBSElementInternalID, 
  
  ess.Batch,     
  ess.InventoryStockType,    
  ess.StockOwner,
    
  // associations
  e._Material,
  ess._Equipment,
  ess._Plant,
  ess._StorageLocation,
  ess._SalesOrder,
  ess._SalesOrderItem,
  ess._Customer,
  ess._CustomerCompanyByPlant,
  ess._Supplier,
  ess._SupplierCompanyByPlant,
  ess._InventoryStockType,
  ess._InventorySpecialStockType,
  ess._WBSElementByInternalKey
}
```
