---
name: I_CNSLDTNDISTRCHANNELHIERARCHY
description: Cnsldtndistrchannelhierarchy
semantic_en: "consolidation distribution channel hierarchy — stores distribution channel hierarchy structures with validity dates and change tracking."
semantic_vi: "phân cấp kênh phân phối hợp nhất — lưu trữ cấu trúc phân cấp kênh phân phối với ngày hiệu lực."
keywords:
  - phân cấp kênh phân phối
  - tính hợp lệ
  - lịch sử thay đổi
app_component: FIN-CS-MD-2CL
software_component: SAPSCORE
release_state: released
clean_core_level: A
system_type: public_cloud
source_available: true
tags:
  - FIN
  - FIN-CS
  - FIN-CS-MD
  - interface-view
  - component:FIN-CS-MD-2CL
  - lob:Other
---
# I_CNSLDTNDISTRCHANNELHIERARCHY

**Cnsldtndistrchannelhierarchy**

| Property | Value |
|---|---|
| App Component | `FIN-CS-MD-2CL` |
| Software Component | `SAPSCORE` |
| Release State | Released (Level A) |
| System Type | S/4HANA Cloud Public Edition |

## Fields

| Field | Data Source |
|---|---|
| `CnsldtnDistrChannelHierarchy` | `cast(…)` |
| `ValidityEndDate` | `_Hierarchy.ValidityEndDate` |
| `ValidityStartDate` | `_Hierarchy.ValidityStartDate` |
| `LastChangedByUser` | `_Hierarchy.LastChangedByUser` |
| `LastChangeDateTime` | `_Hierarchy.LastChangeDateTime` |
| `_Text` | *Association* |

## Associations

| Alias | Target View | Cardinality |
|---|---|---|
| `_Text` | `I_CnsldtnDistrChannelHierT` | [0..*] |

## Source Code

```abap
@Analytics: {
  dataCategory: #DIMENSION,
  dataExtraction.enabled: true,
  internalName: #LOCAL,
  technicalName: 'ICSDISTRCHANNELHIERARCHY'
}

@AccessControl:{
  authorizationCheck: #MANDATORY
}
@Metadata:{
  allowExtensions:true,
  ignorePropagatedAnnotations: true
}
@ObjectModel:{
  usageType: {
    dataClass: #MASTER,
    serviceQuality: #B,
    sizeCategory: #S
  },
  representativeKey: 'CnsldtnDistrChannelHierarchy',
  modelingPattern: #ANALYTICAL_DIMENSION,
  supportedCapabilities: [#CDS_MODELING_DATA_SOURCE,
                          #CDS_MODELING_ASSOCIATION_TARGET,
                          #SQL_DATA_SOURCE,
                          #EXTRACTION_DATA_SOURCE,
                          #ANALYTICAL_DIMENSION ],
  sapObjectNodeType.name: 'CnsldtnDistrChannelHierarchy'
}
@VDM:{
  viewType: #BASIC,
  lifecycle.contract.type: #PUBLIC_LOCAL_API
}
@EndUserText.label: 'Cnsldtn Distribution Channel Hierarchy'
/*+[hideWarning] { "IDS" : [ "KEY_CHECK" ]  } */
define view entity I_CnsldtnDistrChannelHierarchy
  as select from P_CnsldtnUnivHierarchy_3(P_HierarchyType : 'CS28', P_MasterDataType : 'VTWEG' ) as _Hierarchy

  association [0..*] to I_CnsldtnDistrChannelHierT as _Text on $projection.CnsldtnDistrChannelHierarchy = _Text.CnsldtnDistrChannelHierarchy // no validity date condition due to limitation in analytic engine

{
      @ObjectModel.text.association: '_Text'
  key cast(_Hierarchy.UniversalHierarchy as fincs_distrchannelhierarchy preserving type ) as CnsldtnDistrChannelHierarchy,

      @Semantics.businessDate.to: true
  key _Hierarchy.ValidityEndDate,

      @Semantics.businessDate.from: true
      _Hierarchy.ValidityStartDate,

      @Semantics.user.lastChangedBy: true
      _Hierarchy.LastChangedByUser,

      @Semantics.systemDateTime.lastChangedAt: true
      _Hierarchy.LastChangeDateTime,

      /* associations */
      _Text
}
```
