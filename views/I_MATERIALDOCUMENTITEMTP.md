---
name: I_MATERIALDOCUMENTITEMTP
description: Materialdocumentitemtp
semantic_en: "material document item text processing — material movement details with quantities, units, and storage location"
semantic_vi: "chi tiết mục tài liệu vật tư — dữ liệu chuyển động vật tư với số lượng, đơn vị, kho"
keywords:
  - tài liệu vật tư
  - mục
  - chuyển động
app_component: MM-IM-GF-2CL
software_component: SAPSCORE
release_state: released
clean_core_level: A
system_type: public_cloud
source_available: true
tags:
  - MM
  - MM-IM
  - MM-IM-GF
  - interface-view
  - transactional-processing
  - material
  - document
  - item-level
  - component:MM-IM-GF-2CL
  - lob:Sourcing & Procurement
  - bo:Material
---
# I_MATERIALDOCUMENTITEMTP

**Materialdocumentitemtp**

| Property | Value |
|---|---|
| App Component | `MM-IM-GF-2CL` |
| Software Component | `SAPSCORE` |
| Release State | Released (Level A) |
| System Type | S/4HANA Cloud Public Edition |

## Fields

| Field | Data Source |
|---|---|
| `MaterialDocumentYear` | `MaterialDocumentYear` |
| `MaterialDocument` | `MaterialDocument` |
| `MaterialDocumentItem` | `MaterialDocumentItem` |
| `MaterialDocumentLine` | `MaterialDocumentLine` |
| `MaterialDocumentParentLine` | `MaterialDocumentParentLine` |
| `GoodsMovementType` | `GoodsMovementType` |
| `Material` | `Material` |
| `Plant` | `Plant` |
| `StorageLocation` | `StorageLocation` |
| `InventoryUsabilityCode` | `InventoryUsabilityCode` |
| `QuantityInEntryUnit` | `QuantityInEntryUnit` |
| `EntryUnit` | `EntryUnit` |
| `QuantityInBaseUnit` | `QuantityInBaseUnit` |
| `MaterialBaseUnit` | `MaterialBaseUnit` |
| `Batch` | `Batch` |
| `ShelfLifeExpirationDate` | `ShelfLifeExpirationDate` |
| `ManufactureDate` | `ManufactureDate` |
| `InventorySpecialStockType` | `InventorySpecialStockType` |
| `Customer` | `Customer` |
| `Supplier` | `Supplier` |
| `InventoryStockType` | `InventoryStockType` |
| `InventoryValuationType` | `InventoryValuationType` |
| `AccountAssignmentCategory` | `AccountAssignmentCategory` |
| `ReversedMaterialDocumentYear` | `ReversedMaterialDocumentYear` |
| `ReversedMaterialDocument` | `ReversedMaterialDocument` |
| `ReversedMaterialDocumentItem` | `ReversedMaterialDocumentItem` |
| `GoodsMovementIsCancelled` | `GoodsMovementIsCancelled` |
| `FiscalYearPeriod` | `FiscalYearPeriod` |
| `FiscalYearVariant` | `FiscalYearVariant` |
| `IsAutomaticallyCreated` | `IsAutomaticallyCreated` |
| `MaterialDocumentPostingType` | `MaterialDocumentPostingType` |
| `ReservationItemRecordType` | `ReservationItemRecordType` |
| `DebitCreditCode` | `DebitCreditCode` |
| `IssgOrRcvgMaterial` | `IssgOrRcvgMaterial` |
| `IssuingOrReceivingPlant` | `IssuingOrReceivingPlant` |
| `IssuingOrReceivingStorageLoc` | `IssuingOrReceivingStorageLoc` |
| `IssgOrRcvgBatch` | `IssgOrRcvgBatch` |
| `IssgOrRcvgSpclStockInd` | `IssgOrRcvgSpclStockInd` |
| `IssuingOrReceivingValType` | `IssuingOrReceivingValType` |
| `WBSElementInternalID` | `WBSElementInternalID` |
| `Reservation` | `Reservation` |
| `ReservationItem` | `ReservationItem` |
| `GoodsMovementRefDocType` | `GoodsMovementRefDocType` |
| `GoodsMovementReasonCode` | `GoodsMovementReasonCode` |
| `PurchaseOrder` | `PurchaseOrder` |
| `PurchaseOrderItem` | `PurchaseOrderItem` |
| `IsCompletelyDelivered` | `IsCompletelyDelivered` |
| `SerialNumbersAreCreatedAutomly` | `SerialNumbersAreCreatedAutomly` |
| `CostCenter` | `CostCenter` |
| `GLAccount` | `GLAccount` |
| `ProfitCenter` | `ProfitCenter` |
| `SalesOrder` | `SalesOrder` |
| `SalesOrderItem` | `SalesOrderItem` |
| `SalesOrderScheduleLine` | `SalesOrderScheduleLine` |
| `DeliveryDocument` | `DeliveryDocument` |
| `DeliveryDocumentItem` | `DeliveryDocumentItem` |
| `GoodsRecipientName` | `GoodsRecipientName` |
| `UnloadingPointName` | `UnloadingPointName` |
| `ReservationIsFinallyIssued` | `ReservationIsFinallyIssued` |
| `ManufacturingOrder` | `ManufacturingOrder` |
| `ManufacturingOrderItem` | `ManufacturingOrderItem` |
| `SpecialStockIdfgSalesOrder` | `SpecialStockIdfgSalesOrder` |
| `SpecialStockIdfgSalesOrderItem` | `SpecialStockIdfgSalesOrderItem` |
| `MaterialDocumentItemText` | `MaterialDocumentItemText` |
| `GdsMvtExtAmtInCoCodeCrcy` | `GdsMvtExtAmtInCoCodeCrcy` |
| `ProductStandardID` | `ProductStandardID` |
| `CompanyCodeCurrency` | `CompanyCodeCurrency` |
| `ReferenceDocumentFiscalYear` | `ReferenceDocumentFiscalYear` |
| `InvtryMgmtRefDocumentItem` | `InvtryMgmtRefDocumentItem` |
| `InvtryMgmtReferenceDocument` | `InvtryMgmtReferenceDocument` |
| `_GoodsMovementType` | *Association* |
| `_Product` | *Association* |
| `_Plant` | *Association* |
| `_StorageLocation` | *Association* |
| `_GoodsMovementReasonCode` | *Association* |
| `_EntryUnit` | *Association* |
| `_MaterialBaseUnit` | *Association* |

## Associations

> No associations found.

## Source Code

```abap
@AccessControl.authorizationCheck: #MANDATORY
@EndUserText.label: 'Material Document Item - TP'
@ObjectModel.usageType: {
    serviceQuality: #C,
    sizeCategory  : #XXL,
    dataClass     : #TRANSACTIONAL }

@VDM.lifecycle.contract.type: #PUBLIC_LOCAL_API
@VDM.viewType: #TRANSACTIONAL
@ObjectModel.supportedCapabilities: [#TRANSACTIONAL_PROVIDER]
@ObjectModel.modelingPattern: #TRANSACTIONAL_PROJECTED_ENTITY
@Metadata.ignorePropagatedAnnotations: true
define view entity I_MaterialDocumentItemTP
  as projection on R_MaterialDocumentItemTP as _MaterialDocumentItem
{
  key MaterialDocumentYear,
  key MaterialDocument,
  key MaterialDocumentItem,


      MaterialDocumentLine,
      MaterialDocumentParentLine,

      GoodsMovementType,
      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_ProductStdVH', element: 'Product' }, useAsTemplate: true } ]
      Material,
      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_PlantStdVH', element: 'Plant' }, useAsTemplate: true } ]
      Plant,
      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_StorageLocationStdVH', element: 'StorageLocation' }, useAsTemplate: true } ]
      StorageLocation,

      InventoryUsabilityCode,
      @Semantics.quantity.unitOfMeasure: 'EntryUnit'
      QuantityInEntryUnit, // todo: check if they can differ
      EntryUnit,
      @Semantics.quantity.unitOfMeasure: 'MaterialBaseUnit'
      QuantityInBaseUnit, //todo: check if they can differ
      MaterialBaseUnit,
      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_BatchStdVH', element: 'Batch' },
                                      additionalBinding: [{ element: 'Material', localElement: 'Material' },
                                                          { element: 'Plant', localElement: 'Plant' } ], useAsTemplate: true } ]
      Batch,
      ShelfLifeExpirationDate,
      ManufactureDate,
      InventorySpecialStockType,

      @Consumption.valueHelpDefinition: [{entity: { name: 'I_Customer_VH', element: 'Customer' }, useAsTemplate: true } ]
      Customer,
      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_Supplier_VH', element: 'Supplier' }, useAsTemplate: true } ]
      Supplier,

      @Feature: 'MM_IM_MATDOC_PAAS_ENHANCE'
      InventoryStockType,
      @Feature: 'MM_IM_MATDOC_PAAS_ENHANCE'
      InventoryValuationType,
      @Feature: 'MM_IM_MATDOC_PAAS_ENHANCE'
      AccountAssignmentCategory,
      @Feature: 'MM_IM_MATDOC_PAAS_ENHANCE'
      ReversedMaterialDocumentYear,
      @Feature: 'MM_IM_MATDOC_PAAS_ENHANCE'
      ReversedMaterialDocument,
      @Feature: 'MM_IM_MATDOC_PAAS_ENHANCE'
      ReversedMaterialDocumentItem,
      @Feature: 'MM_IM_MATDOC_PAAS_ENHANCE'
      GoodsMovementIsCancelled,
      @Feature: 'MM_IM_MATDOC_PAAS_ENHANCE'
      FiscalYearPeriod,
      @Feature: 'MM_IM_MATDOC_PAAS_ENHANCE'
      FiscalYearVariant,
      @Feature: 'MM_IM_MATDOC_PAAS_ENHANCE'
      @Semantics.booleanIndicator: true
      IsAutomaticallyCreated,
      @Feature: 'MM_IM_MATDOC_PAAS_ENHANCE'
      MaterialDocumentPostingType,
      @Feature: 'MM_IM_MATDOC_PAAS_ENHANCE'
      ReservationItemRecordType,
      @Feature: 'MM_IM_MATDOC_PAAS_ENHANCE'
      DebitCreditCode,
      
      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_ProductStdVH', element: 'Product' }, useAsTemplate: true } ]
      IssgOrRcvgMaterial,
      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_PlantStdVH', element: 'Plant' }, useAsTemplate: true } ]
      IssuingOrReceivingPlant,
      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_StorageLocationStdVH', element: 'StorageLocation' }, useAsTemplate: true,
      additionalBinding: [{ element: 'Plant', localElement: 'IssuingOrReceivingPlant' }]
      } ]
      IssuingOrReceivingStorageLoc,
      IssgOrRcvgBatch,
      IssgOrRcvgSpclStockInd,
      IssuingOrReceivingValType,

      WBSElementInternalID,
      Reservation,
      ReservationItem,

      GoodsMovementRefDocType, // todo: check if can be determined based on goodsmvtcode
      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_GoodsMovementReasonCode', element: 'GoodsMovementReasonCode' },useAsTemplate: true, additionalBinding: [{ element: 'GoodsMovementType', localElement: 'GoodsMovementType' }] } ]
      GoodsMovementReasonCode,

      PurchaseOrder,
      PurchaseOrderItem,
      IsCompletelyDelivered,

      SerialNumbersAreCreatedAutomly,

      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_CostCenterStdVH', element: 'CostCenter' }, useAsTemplate: true } ]
      CostCenter,
      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_GLAccountStdVH', element: 'GLAccount' }, useAsTemplate: true } ]
      GLAccount,
      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_ProfitCenterStdVH', element: 'ProfitCenter' },useAsTemplate: true } ]
      ProfitCenter,
      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_SalesOrderStdVH', element: 'SalesOrder' },useAsTemplate: true } ]
      SalesOrder,
      @Consumption.valueHelpDefinition: [ { entity: { name: 'I_SalesOrderItemStdVH', element: 'SalesOrderItem' }, additionalBinding: [{ localElement: 'SalesOrder', element: 'SalesOrder' }],useAsTemplate: true } ]
      SalesOrderItem,
      SalesOrderScheduleLine,
      @Consumption.valueHelpDefinition: [ { entity:  { name: 'I_DeliveryDocumentStdVH', element: 'DeliveryDocument' }, useAsTemplate: true }]
      DeliveryDocument,
      DeliveryDocumentItem,
      GoodsRecipientName,
      UnloadingPointName,
      ReservationIsFinallyIssued,
      ManufacturingOrder,
      ManufacturingOrderItem,

      SpecialStockIdfgSalesOrder,
      SpecialStockIdfgSalesOrderItem,

      MaterialDocumentItemText,
      @Semantics.amount.currencyCode: 'CompanyCodeCurrency'
      GdsMvtExtAmtInCoCodeCrcy,
      ProductStandardID,
      CompanyCodeCurrency,

      ReferenceDocumentFiscalYear,
      InvtryMgmtRefDocumentItem,
      InvtryMgmtReferenceDocument,

      //Associations
      _GoodsMovementType,
      _Product,
      _Plant,
      _StorageLocation,
      _GoodsMovementReasonCode,
      _EntryUnit,
      _MaterialBaseUnit,


      _MaterialDocumentHeader : redirected to parent I_MaterialDocumentTP,
      _SerialNumber           : redirected to composition child I_MaterialDocItemSerialNoTP

      //      HierarchyNodeLevel, //todo: check if needed, ATC
      //      StorageType, todo: check old wm still supported
      //      StorageBin,  todo: check old wm still supported
      //      IssgOrRcvgStockSegment,
      //      StockSegment,
      //      ProfitabilitySegment, ATC
      //      CostObject, // todo: check if needed
      //      BusinessArea,
}
```
